# converted with mingw-to-msys from MINGW-packages/mingw-w64-python-cryptography/PKGBUILD https://github.com/msys2/MINGW-packages/blob/2475e24a310fa950e66ad6c1a1c5667a7c0103b9/mingw-w64-python-cryptography/PKGBUILD
# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_with_rust="no"

_realname=cryptography
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=3.4.7
pkgrel=1
pkgdesc="A package designed to expose cryptographic recipes and primitives to Python developers (mingw-w64)"
url='https://pypi.org/project/cryptography/'
license=('Apache')
arch=('i686' 'x86_64')
depends=("python-cffi"
         "python-idna"
         "openssl-devel")
makedepends=("python-setuptools")
if [ "${_with_rust}" == "yes" ]; then
  makedepends+=("python-setuptools-rust")
else
  export CRYPTOGRAPHY_DONT_BUILD_RUST=1
fi
checkdepends=("python-pytest-runner"
              #"python-iso8601"
              "python-pretend"
              #"python-cryptography-vectors"
              "python-hypothesis"
              "python-pytz")
source=(https://pypi.io/packages/source/c/cryptography/${_realname}-${pkgver}.tar.gz
        0002-move-setuptools-rust-import.patch)
sha256sums=('3d10de8116d25649631977cb37da6cbdd2d6fa0e0281d014a5b7d337255ca713'
            '7374d57ae28eefe78394f2d6d983adf7d216c040c3b4bac8e83e4f5132c51fa2')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # don't fail for lack of setuptools-rust if not building rust
  patch -Np1 -i "${srcdir}"/0002-move-setuptools-rust-import.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # /usr/bin/python setup.py pytest
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}/share/licenses/python-${_realname}/LICENSE"
}
